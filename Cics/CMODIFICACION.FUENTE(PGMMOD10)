       IDENTIFICATION DIVISION.
       PROGRAM-ID. PGMMOD10.
       DATA DIVISION.
       FILE SECTION.
       WORKING-STORAGE SECTION.
       01 CT-CONSTANTES.
          03 CT-MSGO.
             05 CT-MNS-INICIO         PIC X(72)        VALUE
                  'INGRESE LOS DATOS Y PRESIONE ENTER'.
             05 CT-MNS-ERRORCLIE      PIC X(72)        VALUE
                  'ERROR - CLIENTE INEXISTENTE'.
             05 CT-MNS-ER-AMB         PIC X(72)        VALUE
                  'TIPO Y NÚMERO DOCUMENTO INEXISTENTES - REINGRESE'.
             05 CT-MNS-ER-TIP-DOC     PIC X(72)        VALUE
                  'TIPO DE DOCUMENTO INVALIDO'.
             05 CT-MNS-ER-NRO-DOC     PIC X(72)        VALUE
                  'NUMERO DE DOCUMENTO INVALIDO'.
             05 CT-MSN-ER-NOMAPE      PIC X(72)        VALUE
                  'ERROR, NOMBRE INGRESADO INVáLIDO'.
             05 CT-MNS-ER-FECNAC      PIC X(72)        VALUE
                  'ERROR, FECHA DE NACIMIENTO INVÁLIDA'.
             05 CT-MNS-ER-SEXO        PIC X(72)        VALUE
                  'ERROR, SEXO INGRESADO INVÁLIDO'.
             05 CT-ER-MNS-ARCHPER     PIC X(72)        VALUE
                  'PROBLEMA CON ARCHIVO PERSONA'.
             05 CT-MNS-INVALID        PIC X(72)        VALUE
                  'TECLA INVALIDA, VUELVA A BUSCAR Y REINTENTE'.
             05 CT-MNS-MOD-EXITO      PIC X(72)        VALUE
                  'MODIFICACIóN REALIZADA CON ÉXITO'.
             05 CT-MNS-CLIENTE-FND    PIC X(72)        VALUE
                  'CLIENTE ENCONTRADO - MODIFIQUE Y PRESIONE ENTER'.
             05 CT-MNS-SINCAMBIOS     PIC X(72)        VALUE
                  'NO SE PRESENTARON CAMBIOS'.
             05 CT-MNS-EXIT           PIC X(72)        VALUE
                  'FIN DE TRANSACCION T510'.
          03 CT-DATASET               PIC X(08)        VALUE 'PERSONA'.
          03 CT-DATASET-LEN           PIC S9(04) COMP  VALUE 160.

       01 WS-VARIABLES.
          03 WS-MAP-00                PIC X(07)        VALUE 'MAP0510'.
          03 WS-MAPSET-00             PIC X(07)        VALUE 'MAP0510'.
          03 WS-LONG                  PIC S9(04) COMP.
          03 WS-ABSTIME               PIC S9(16) COMP  VALUE +0.
          03 WS-FECHA                 PIC X(10)        VALUE SPACES.
          03 WS-SEP-DATE              PIC X            VALUE '/'.
          03 WS-HORA                  PIC X(08)        VALUE SPACES.
          03 WS-SEP-HOUR              PIC X            VALUE ':'.
          03 WS-RESP                  PIC S9(04) COMP.
          03 WS-ERR                   PIC X(15).
          03 SW-CONFIRMAR             PIC X            VALUE 'Y'.
          03 WS-NORMAL                PIC X            VALUE '*'.
          03 WS-ENTER                 PIC X            VALUE ' '.
          03 WS-FECHA-SAL             PIC 9(08)        VALUE ZEROES.
          03 WS-RESULTADO             PIC 9(04)        VALUE ZEROES.
          03 WS-RESTO-4               PIC 9(04)V99     VALUE ZEROES.
          03 WS-RESTO-100             PIC 9(02)V99     VALUE ZEROES.
          03 WS-RESTO-400             PIC 9(02)V99     VALUE ZEROES.

       01 WS-COMMAREA.
          03 WS-USER-DATA.
             05 WS-USER-TIPDOC        PIC X(02).
             05 WS-USER-NRODOC        PIC 9(11).
          03 WS-TIP-DOC               PIC X(02).
             88 WS-TIP-DOC-BOOLEAN                     VALUE 'DU'
                                                             'PA'
                                                             'PE'.
          03 WS-SEXO                  PIC X.
             88 WS-SEXO-VALIDO                         VALUE 'F'
                                                             'M'
                                                             'O'.
          03 FILLER                   PIC X(5).

       01  REG-PERSONA.
           03  PER-TIP-DOC            PIC X(02).
           03  PER-NRO-DOC            PIC 9(11).
           03  PER-CLI-NRO            PIC 9(03).
           03  PER-NOMAPE             PIC X(30).
           03  PER-CLI-AAAAMMDD       PIC 9(08).
           03  PER-DIRECCION          PIC X(30).
           03  PER-LOCALIDAD          PIC X(15).
           03  PER-EMAIL              PIC X(30).
           03  PER-TELEFONO           PIC X(15).
           03  PER-SEXO               PIC X.
           03  PER-NRO-SUCURSAL       PIC 9(03).
           03  FILLER                 PIC X(12).

       01 FLAG-VALIDAR                PIC X.
          88 REG-VALIDO                                VALUE 'Y'.
          88 REG-FAIL                                  VALUE 'N'.

       01 FLAG-VALIDAR-ENTER          PIC X.
          88 DATA-VALIDA                               VALUE 'Y'.
          88 DATA-FAIL                                 VALUE 'N'.

       COPY MAP0510.
       COPY DFHBMSCA.
       COPY DFHAID.

       PROCEDURE DIVISION.

       MAIN-PROGRAM.

           PERFORM 1000-I-INICIO
              THRU 1000-I-INICIO.
           PERFORM 2000-I-PROCESO
              THRU 2000-F-PROCESO.

       1000-I-INICIO.

           IF EIBCALEN = 0 OR DFHAID = DFHPF3
               MOVE LOW-VALUES TO MAP0510O
               MOVE LOW-VALUES TO WS-USER-DATA
               MOVE LENGTH OF MAP0510O TO WS-LONG
               PERFORM 7000-I-TIME
                  THRU 7000-F-TIME

               PERFORM 2100-I-OCULTAR-CAMPOS
                  THRU 2100-F-OCULTAR-CAMPOS

               MOVE 'T510-MAP0510' TO T510O
               MOVE CT-MNS-INICIO TO MSGO
               EXEC CICS
                    SEND MAP (WS-MAP-00)
                         MAPSET (WS-MAPSET-00)
                         FROM (MAP0510O)
                         LENGTH (WS-LONG)
                         ERASE
                         FREEKB
                         RESP (WS-RESP)
               END-EXEC
               IF WS-RESP = DFHRESP(NORMAL)
                 PERFORM 9999-I-FINAL
                    THRU 9999-F-FINAL
               ELSE
                 MOVE 'ERROR EIBCALEN' TO WS-ERR
                 EXEC CICS SEND TEXT FROM (WS-ERR) END-EXEC
                 PERFORM 9000-I-PF12
                    THRU 9000-F-PF12.

       1000-F-INICIO. EXIT.

       2000-I-PROCESO.

           PERFORM 7000-I-TIME
              THRU 7000-F-TIME
           MOVE LENGTH OF MAP0510O TO WS-LONG
           EXEC CICS
                RECEIVE MAP    (WS-MAP-00)
                     MAPSET (WS-MAPSET-00)
                       INTO     (MAP0510I)
                       RESP      (WS-RESP)
           END-EXEC.
           IF WS-RESP = DFHRESP(NORMAL)
              PERFORM 2500-I-PULSAR-TECLA
                 THRU 2500-F-PULSAR-TECLA
           ELSE
              MOVE 'ERROR 2000-PROC' TO WS-ERR
              EXEC CICS SEND TEXT FROM(WS-ERR) END-EXEC
              PERFORM 9000-I-PF12
                 THRU 9000-F-PF12
           END-IF.

       2000-F-PROCESO. EXIT.

       2100-I-OCULTAR-CAMPOS.

      * PROPIEDAD PARA TEXTO NO EDITABLE.*

           MOVE DFHPROTN TO NOMAPSA.
           MOVE DFHPROTN TO FECNACA.
           MOVE DFHPROTN TO FECSEP1A.
           MOVE DFHPROTN TO FECSEP2A.
           MOVE DFHPROTN TO SEXSA.

      * PROPIEDAD PARA TEXTO EDITABLE *

           MOVE DFHPROTN TO NOMAPEA.
           MOVE DFHPROTN TO FECDIAA.
           MOVE DFHPROTN TO FECMESA.
           MOVE DFHPROTN TO FECANIOA.
           MOVE DFHPROTN TO SEXOA.

       2100-F-OCULTAR-CAMPOS. EXIT.

       2200-I-MOSTRAR-CAMPOS.

           MOVE -1 TO NOMAPEL.

      * VUELVE VISIBLE LOS CAMPOS NO EDITABLES. *

           MOVE DFHDFHI TO NOMAPSA.
           MOVE DFHDFHI TO FECNACA.
           MOVE DFHDFHI TO FECSEP1A.
           MOVE DFHDFHI TO FECSEP2A.
           MOVE DFHDFHI TO SEXSA.

      * VUELVE VISIBLE LOS CAMPOS EDITABLES. *

           MOVE DFHDFHI TO NOMAPEA.
           MOVE DFHDFHI TO FECDIAA.
           MOVE DFHDFHI TO FECMESA.
           MOVE DFHDFHI TO FECANIOA.
           MOVE DFHDFHI TO SEXOA.

       2200-F-MOSTRAR-CAMPOS. EXIT.

       2500-I-PULSAR-TECLA.

             EVALUATE EIBAID
                WHEN DFHENTER
                   PERFORM 3000-I-ENTER
                      THRU 3000-F-ENTER
                WHEN DFHPF3
                   PERFORM 3500-I-PF3
                      THRU 3500-F-PF3
                WHEN DFHPF12
                   PERFORM 9000-I-PF12
                      THRU 9000-F-PF12
                WHEN OTHER
                MOVE LOW-VALUES     TO MAP0510I
                MOVE LOW-VALUES     TO MAP0510O
                MOVE LOW-VALUES     TO WS-USER-DATA
                PERFORM 7000-I-TIME
                THRU 7000-F-TIME
                MOVE 'T510-MAP0510' TO T510O
                MOVE -1             TO TIPDOCL
                MOVE CT-MNS-INVALID TO MSGO
                PERFORM 2100-I-OCULTAR-CAMPOS
                   THRU 2100-F-OCULTAR-CAMPOS
             END-EVALUATE.
             IF WS-USER-DATA IS EQUAL LOW-VALUES
                PERFORM 2100-I-OCULTAR-CAMPOS
                   THRU 2100-F-OCULTAR-CAMPOS
             END-IF.

             PERFORM 7000-I-TIME
                THRU 7000-F-TIME.
             MOVE 'T510-MAP0510' TO T510O.
             EXEC CICS
                SEND MAP    (WS-MAP-00)
                MAPSET (WS-MAPSET-00)
                FROM   (MAP0510O)
                LENGTH (WS-LONG)
                ERASE
                FREEKB
                RESP (WS-RESP)
                CURSOR
             END-EXEC.
             IF WS-RESP = DFHRESP(NORMAL)
                PERFORM 9999-I-FINAL
                   THRU 9999-F-FINAL
             ELSE
                MOVE 'ERROR 2500-TECLA' TO WS-ERR
                EXEC CICS SEND TEXT FROM (WS-ERR) END-EXEC
                PERFORM 9000-I-PF12
                   THRU 9000-F-PF12
             END-IF.

       2500-F-PULSAR-TECLA. EXIT.

       3000-I-ENTER.

           SET REG-VALIDO  TO TRUE.
           SET DATA-VALIDA TO TRUE.

           IF NUMDOCI IS NOT NUMERIC
              MOVE CT-MNS-ER-NRO-DOC TO MSGO
              MOVE -1                TO NUMDOCL
              MOVE LOW-VALUES TO WS-USER-DATA
              PERFORM 2100-I-OCULTAR-CAMPOS
                 THRU 2100-F-OCULTAR-CAMPOS
              SET DATA-FAIL TO TRUE
           END-IF.

           MOVE TIPDOCI TO WS-TIP-DOC.
           IF NOT WS-TIP-DOC-BOOLEAN
              MOVE CT-MNS-ER-TIP-DOC TO MSGO
              MOVE -1                TO TIPDOCL
              MOVE LOW-VALUES TO WS-USER-DATA
              PERFORM 2100-I-OCULTAR-CAMPOS
                 THRU 2100-F-OCULTAR-CAMPOS
              SET DATA-FAIL TO TRUE
           END-IF.

              IF DATA-VALIDA
                 IF (TIPDOCI IS EQUAL WS-USER-TIPDOC) AND
                    (NUMDOCI IS EQUAL WS-USER-NRODOC)
                    PERFORM 6000-I-VALIDACION
                       THRU 6000-F-VALIDACION
                    IF REG-VALIDO
                       PERFORM 5100-I-ESCRITURA
                          THRU 5100-F-ESCRITURA
                    END-IF
                 ELSE
                    MOVE TIPDOCI TO WS-USER-TIPDOC
                    MOVE NUMDOCI TO WS-USER-NRODOC
                    PERFORM 3100-I-LECTURA
                       THRU 3100-F-LECTURA
                 END-IF
              END-IF.

       3000-F-ENTER. EXIT.

       3100-I-LECTURA.

           MOVE LOW-VALUES TO MAP0510O.
           PERFORM 7000-I-TIME
              THRU 7000-F-TIME.
           EXEC CICS
                READ DATASET (CT-DATASET)
                RIDFLD  (WS-USER-DATA)
                INTO (REG-PERSONA)
                LENGTH (CT-DATASET-LEN)
                EQUAL
                RESP (WS-RESP)
           END-EXEC.
           EVALUATE WS-RESP
              WHEN DFHRESP(NORMAL)
                 MOVE PER-TIP-DOC             TO TIPDOCO
                 MOVE PER-NRO-DOC             TO NUMDOCO
                 MOVE PER-NOMAPE              TO NOMAPEO
                 MOVE PER-CLI-AAAAMMDD(1:4)   TO FECANIOO
                 MOVE PER-CLI-AAAAMMDD(5:2)   TO FECMESO
                 MOVE PER-CLI-AAAAMMDD(7:2)   TO FECDIAO
                 MOVE PER-SEXO                TO SEXOO
                 MOVE LENGTH OF MAP0510O      TO WS-LONG
                 MOVE CT-MNS-CLIENTE-FND      TO MSGO
                 PERFORM 2200-I-MOSTRAR-CAMPOS
                    THRU 2200-F-MOSTRAR-CAMPOS
              WHEN DFHRESP(NOTFND)
                 MOVE WS-USER-TIPDOC          TO TIPDOCO
                 MOVE WS-USER-NRODOC          TO NUMDOCO
                 MOVE LENGTH OF MAP0510O      TO WS-LONG
                 MOVE CT-MNS-ER-AMB           TO MSGO
                 MOVE LOW-VALUES TO WS-USER-DATA
                 PERFORM 2100-I-OCULTAR-CAMPOS
                    THRU 2100-F-OCULTAR-CAMPOS
              WHEN OTHER
                 MOVE LOW-VALUES              TO WS-USER-DATA
                 MOVE LOW-VALUES              TO MAP0510I
                 MOVE LENGTH OF MAP0510O      TO WS-LONG
                 MOVE CT-ER-MNS-ARCHPER       TO MSGO
                 PERFORM 2100-I-OCULTAR-CAMPOS
                    THRU 2100-F-OCULTAR-CAMPOS
           END-EVALUATE.

       3100-F-LECTURA. EXIT.

       3500-I-PF3.

           MOVE LOW-VALUES TO MAP0510I.
           MOVE LOW-VALUES TO MAP0510O.
           MOVE LOW-VALUES TO WS-USER-DATA.
           MOVE CT-MNS-INICIO  TO MSGO.
           MOVE -1 TO TIPDOCL.

       3500-F-PF3. EXIT.

       5100-I-ESCRITURA.

           PERFORM 4200-I-VERIFICAR-DATOS
              THRU 4200-F-VERIFICAR-DATOS.

           STRING FECANIOI DELIMITED BY SIZE
                  FECMESI  DELIMITED BY SIZE
                  FECDIAI  DELIMITED BY SIZE
                  INTO WS-FECHA-SAL.

           IF (NOMAPEI = PER-NOMAPE AND
               SEXOI   = PER-SEXO   AND
               WS-FECHA-SAL = PER-CLI-AAAAMMDD)
               MOVE CT-MNS-SINCAMBIOS TO MSGO
               MOVE -1        TO NOMAPEL
           ELSE
               MOVE NOMAPEI      TO PER-NOMAPE
               MOVE WS-FECHA-SAL TO PER-CLI-AAAAMMDD
               MOVE SEXOI        TO PER-SEXO
               EXEC CICS REWRITE
                    FILE   (CT-DATASET)
                    FROM   (REG-PERSONA)
                    LENGTH (CT-DATASET-LEN)
               END-EXEC
               EVALUATE WS-RESP
                   WHEN DFHRESP(NORMAL)
                        MOVE CT-MNS-MOD-EXITO TO MSGO
                        MOVE -1 TO NOMAPEL
                   WHEN OTHER
                        MOVE LOW-VALUES TO MAP0510I
                        MOVE CT-ER-MNS-ARCHPER TO MSGO
                        MOVE -1         TO TIPDOCL
               END-EVALUATE
           END-IF.

       5100-F-ESCRITURA. EXIT.

       4200-I-VERIFICAR-DATOS.

           EXEC CICS
             READ DATASET (CT-DATASET)
                  RIDFLD  (WS-USER-DATA)
                  INTO    (REG-PERSONA)
                  LENGTH  (CT-DATASET-LEN)
                  RESP    (WS-RESP)
                  UPDATE
                  EQUAL
           END-EXEC.

           EVALUATE WS-RESP
              WHEN DFHRESP(NORMAL)
                   CONTINUE
              WHEN DFHRESP(NOTFND)
                   MOVE LOW-VALUES TO MAP0510I
                   MOVE TIPDOCI   TO WS-USER-TIPDOC
                   MOVE NUMDOCI   TO WS-USER-NRODOC
                   MOVE CT-MNS-ERRORCLIE TO MSGO
                   MOVE -1 TO TIPDOCL
                   PERFORM 2100-I-OCULTAR-CAMPOS
                      THRU 2100-F-OCULTAR-CAMPOS
              WHEN OTHER
                   MOVE LOW-VALUES TO MAP0510I
                   MOVE CT-ER-MNS-ARCHPER TO MSGO
                   PERFORM 2100-I-OCULTAR-CAMPOS
                      THRU 2100-F-OCULTAR-CAMPOS
              END-EVALUATE.

       4200-F-VERIFICAR-DATOS. EXIT.

       6000-I-VALIDACION.

           MOVE SEXOI TO WS-SEXO.
           IF NOT WS-SEXO-VALIDO
              MOVE CT-MNS-ER-SEXO TO MSGO
              MOVE -1      TO SEXOL
              SET REG-FAIL TO TRUE
           END-IF.

           IF PER-CLI-AAAAMMDD IS EQUAL ZEROES OR PER-CLI-AAAAMMDD IS
              NOT NUMERIC
              MOVE CT-MNS-ER-FECNAC TO MSGO
              MOVE -1      TO FECDIAL
              SET REG-FAIL TO TRUE
           ELSE
              IF FECANIOI < 1940 OR FECANIOI > 2003
                 MOVE CT-MNS-ER-FECNAC TO MSGO
                 MOVE -1      TO FECANIOL
                 SET REG-FAIL TO TRUE
              END-IF
              IF FECMESI < 01 OR FECMESI > 12
                 MOVE CT-MNS-ER-FECNAC TO MSGO
                 MOVE -1      TO FECMESL
                 SET REG-FAIL TO TRUE
              END-IF
              IF FECDIAI < 01 OR FECDIAI > 31
                 MOVE CT-MNS-ER-FECNAC TO MSGO
                 MOVE -1      TO FECDIAL
                 SET REG-FAIL TO TRUE
              END-IF
              IF FECMESI = 02 AND FECDIAI > 28
                 IF FECMESI > 29
                    MOVE CT-MNS-ER-FECNAC TO MSGO
                    MOVE -1      TO FECMESL
                    SET REG-FAIL TO TRUE
                 ELSE
                    DIVIDE FECANIOI BY 004 GIVING WS-RESULTADO
                           REMAINDER WS-RESTO-4
                    DIVIDE FECANIOI BY 100 GIVING WS-RESULTADO
                           REMAINDER WS-RESTO-100
                    DIVIDE FECANIOI BY 400 GIVING WS-RESULTADO
                           REMAINDER WS-RESTO-400
                    IF NOT ((WS-RESTO-4 = 0 AND WS-RESTO-100 NOT = 0)
                       OR WS-RESTO-400 = 0)
                       MOVE CT-MNS-ER-FECNAC TO MSGO
                       MOVE -1      TO FECMESL
                       SET REG-FAIL TO TRUE
                    END-IF
                 END-IF
              END-IF
           END-IF.

           IF NOMAPEI IS EQUAL LOW-VALUES OR NOMAPEI IS EQUAL SPACES
              MOVE CT-MSN-ER-NOMAPE TO MSGO
              MOVE -1 TO NOMAPEL
              SET REG-FAIL TO TRUE
           END-IF.

           IF PER-NRO-DOC IS EQUAL SPACES OR PER-NRO-DOC IS NOT NUMERIC
              MOVE CT-MNS-ER-NRO-DOC TO MSGO
              MOVE -1      TO NUMDOCL
              SET REG-FAIL TO TRUE
           END-IF.

           MOVE TIPDOCI TO WS-TIP-DOC.
           IF NOT WS-TIP-DOC-BOOLEAN
              MOVE CT-MNS-ER-TIP-DOC TO MSGO
              MOVE -1        TO TIPDOCL
              SET REG-FAIL TO TRUE
           END-IF.

       6000-F-VALIDACION. EXIT.

       7000-I-TIME.

           EXEC CICS ASKTIME
             ABSTIME (WS-ABSTIME)
           END-EXEC.
           EXEC CICS FORMATTIME
             ABSTIME (WS-ABSTIME)
             DDMMYYYY (WS-FECHA) DATESEP(WS-SEP-DATE)
             TIME (WS-HORA) TIMESEP(WS-SEP-HOUR)
           END-EXEC.
           MOVE WS-FECHA TO FECHAO.

       7000-F-TIME. EXIT.

       9000-I-PF12.

           EXEC CICS
                SEND CONTROL ERASE
           END-EXEC.
           EXEC CICS
                XCTL PROGRAM('PGMMEN10')
           END-EXEC.
           EXEC CICS
                RETURN
           END-EXEC.

       9000-F-PF12. EXIT.

       9999-I-FINAL.

           EXEC CICS
                RETURN
                TRANSID  ('T510')
                COMMAREA (WS-COMMAREA)
           END-EXEC.

       9999-F-FINAL. EXIT.
